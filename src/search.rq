PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX obo:  <http://purl.obolibrary.org/obo/>
PREFIX MRO:  <http://purl.obolibrary.org/obo/MRO_>

SELECT DISTINCT
  ?subject
  ?restriction_level
  ?mhc_allele_restriction_id
  ?organism_taxon_id
  ?organism_label
  ?class
  ?locus
  ?haplotype
  ?serotype
  ?label
  ?short_id
  ?namespace
WHERE {
  ?subject
    rdfs:subClassOf* obo:GO_0032991 ;
    rdfs:label ?label .
  OPTIONAL {
    ?subject MRO:has-iedb-mhc-id ?mhc_allele_restriction_id ;
  }
  OPTIONAL {
    ?subject MRO:0001984 ?restriction_level . # has MHC restriciton level
  }

  # Get taxon information, default to "organism"
  OPTIONAL {
    ?subject
      rdfs:subClassOf* ?taxon_protein .
    ?taxon_protein
      owl:equivalentClass / owl:intersectionOf / rdf:rest* / rdf:first ?in_taxon .
    ?in_taxon
      owl:onProperty obo:RO_0002162 ; # in taxon
      owl:someValuesFrom ?taxon .
    ?taxon
      rdfs:label ?taxon_label .
  }
  BIND(REPLACE(STR(?taxon), "http://purl.obolibrary.org/obo/NCBITaxon_", "") AS ?taxon_id)
  BIND(IF(BOUND(?taxon_id), ?taxon_id, "1") AS ?organism_taxon_id)
  BIND(IF(BOUND(?taxon_label), ?taxon_label, "organism") AS ?organism_label)

  # Get class information
  OPTIONAL {
    VALUES (?class_iri ?class) {
      (MRO:0001355 "MHC class I")
      (MRO:0001356 "MHC class II")
      (MRO:0001464 "non-classical MHC")
      (MRO:0001655 "MHC class I") # serotype
      (MRO:0001656 "MHC class II") # serotype
      (MRO:0001718 "MHC class I") # mutant
      (MRO:0001719 "MHC class II") # mutant
      (MRO:0001734 "non-classical MHC") # mutant
    }
    ?subject rdfs:subClassOf* ?class_iri .
  }

  OPTIONAL {
    ?subject MRO:has-mhc-locus ?locus .
  }

  # Get haplotype
  OPTIONAL {
    FILTER (?subject != MRO:0000010) # not MHC protein complex with haplotype
    ?subject
      (rdfs:subClassOf / owl:someValuesFrom)? / # handle mutants
        owl:equivalentClass / owl:intersectionOf / rdf:rest* / rdf:first
          ?haplotype_member .
    ?haplotype_member
      owl:onProperty MRO:0000000 ; # haplotype member of
      owl:someValuesFrom ?haplotype_iri .
    ?haplotype_iri
      rdfs:label ?haplotype .
  }

  # Get serotype
  OPTIONAL {
    FILTER (?subject != MRO:0000011) # not MHC protein complex with serotype
    ?subject
      (rdfs:subClassOf / owl:someValuesFrom)? / # handle mutants
        owl:equivalentClass / owl:intersectionOf / rdf:rest* / rdf:first
          ?serotype_member .
    ?serotype_member
      owl:onProperty MRO:0000001 ; # serotype member of
      owl:someValuesFrom ?serotype_iri .
    ?serotype_iri
      rdfs:label ?serotype .
  }

  BIND(REPLACE(STR(?subject), "http://purl.obolibrary.org/obo/", "") AS ?short_id)
  BIND("http://purl.obolibrary.org/obo/" AS ?namespace)
}
ORDER BY ?subject


